(*
Copyright 2018

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*)
theory buddy_large_avail_mem
  imports buddy
begin

context assembly
begin

interpretation until_ret: cfg_system step \<open>until {(ts_ret, pc_ret)}\<close> wf_state location
  by standard (simp add: wf_state_def det_system.is_weak_invar_def)

declare until_def[simp]
declare wf_state_def[simp]
declare location_def[simp]

text \<open>
  This method runs symbolic execution per instruction.
  It unfolds the step function, fetches the next instruction,
  inserts master-information and then simplifies until no further let's are in the goal.
  Then, the masters are cleaned up again (to prevent littering of the subgoal).
\<close>
method symbolic_execution uses masters add del =
  (rule wps_rls)?,
  (simp del: del)?,
  simp only: assembly_def cong: Let'_weak_cong,
  rewrite_one_let',
  rewrite_one_let' del: del,
  rewrite_one_let' add: exec_instr.simps del: del,
  rewrite_one_let' add: unfold_semantics del: del,
  insert masters,
  (rewrite_one_let' add: add del: del)+,
  (thin_tac \<open>master _ _ _\<close>)+,
  rule until_ret.linvar_unfoldI,
  simp del: del

text \<open>
  Symbolic execution can produce multiple subgoals (e.g., due to conditional jumps).
  If one subgoal is finished, call this method once to restart the symbolic execution on the unfinished
  next subgoal.
\<close>
method restart_symbolic_execution uses add del =
  (rewrite_one_let' add: add del: del)+,
  (thin_tac \<open>master _ _ _\<close>)+,
  rule until_ret.linvar_unfoldI,
  simp del: del
|
  ((thin_tac \<open>master _ _ _\<close>)+)?,
  rule until_ret.linvar_unfoldI,
  simp del: del

text \<open>If symbolic execution is finished, the subgoal often contains a large term, possibly with let's.
  This can be simplified by inserting information on the masters and running simplification.
\<close>
method finish_symbolic_execution uses add del masters =
  insert masters,
  simp add: simp_rules add del: del,
  (rewrite_one_let' add: add del: del)+,
  (simp add: simp_rules add del: del; fail)?,
  ((thin_tac \<open>master _ _ _\<close>)+)?

text \<open>
  The Floyd invariant expresses for some locations properties that are invariably true.
  Simply expresses that a byte in the memory remains untouched.
\<close>
definition pp_\<Theta> :: \<open>_ \<Rightarrow> _ \<Rightarrow> _ \<Rightarrow> _ \<Rightarrow> _ \<Rightarrow> _ \<Rightarrow> floyd_invar\<close> where
  \<open>pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret \<equiv> [
    \<comment> \<open>precondition\<close>
    (0, 0) \<mapsto> \<lambda>\<sigma>. RSP \<sigma> = rsp\<^sub>0
                \<and> \<sigma> \<turnstile> *[a,1] = v\<^sub>0
                \<and> \<sigma> \<turnstile> *[rsp\<^sub>0,8] = large_avail_ret,
    \<comment> \<open>loop invariant\<close>
    (0, 8) \<mapsto> \<lambda>\<sigma>. RBP \<sigma> = rsp\<^sub>0 - 8
                \<and> RSP \<sigma> = RBP \<sigma>
                \<and> \<sigma> \<turnstile> *[a,1] = v\<^sub>0
                \<and> \<sigma> \<turnstile> *[rsp\<^sub>0,8] = large_avail_ret,
    \<comment> \<open>postcondition\<close>
    (ts_ret, pc_ret - 1) \<mapsto> \<lambda>\<sigma>. \<sigma> \<turnstile> *[a,1] = v\<^sub>0
                              \<and> RSP \<sigma> = rsp\<^sub>0 + 8
  ]\<close>

schematic_goal pp_\<Theta>_zero[simp]: \<comment> \<open>@{term 0} is not a numeral\<close>
  shows \<open>pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret (0, 0) = ?x\<close>
  unfolding pp_\<Theta>_def
  by simp
schematic_goal pp_\<Theta>_one[simp]: \<comment> \<open>@{term 1} is not a numeral\<close>
  shows \<open>pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret (0, 1) = ?x\<close>
  unfolding pp_\<Theta>_def
  by simp
schematic_goal pp_\<Theta>_numeral[simp]:
  shows \<open>pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret (0, numeral m) = ?x\<close>
  unfolding pp_\<Theta>_def
  by simp
schematic_goal pp_\<Theta>_ret[simp]:
  shows \<open>pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret (ts_ret, pc_ret - 1) = ?x\<close>
  unfolding pp_\<Theta>_def
  by simp

method word_strict_trans uses assms =
  match conclusion in \<open>(w::_::len word) - a < w + b\<close> for w a b \<Rightarrow> \<open>
    rule order.strict_trans[of _ w],
    (insert assms)[1], (* using [1] seems to restrict to one (the first) subgoal *)
    unat_arith,
    simp add: assms
  \<close>

lemma rewrite_buddy_large_avail:
  assumes \<open>seps blocks\<close>
      and \<open>(2, rsp\<^sub>0 - 12, 12) \<in> blocks\<close>
      and \<open>(3, buddy_lists, BUDDY_LISTS * 8) \<in> blocks\<close>
      and \<open>(4, a, 1) \<in> blocks\<close>
      and \<open>(5, rsp\<^sub>0, 8) \<in> blocks\<close>
      and \<open>12 \<le> rsp\<^sub>0\<close>
      and \<open>rsp\<^sub>0 \<le> max_word - 8\<close>
      and \<open>label_to_index \<alpha> (the (address_to_label large_avail_ret)) = Some (ts_ret, pc_ret)\<close>
      and \<open>ts_ret \<noteq> 0\<close>
    shows \<open>until_ret.is_std_invar ts_ret pc_ret (until_ret.floyd.invar ts_ret pc_ret (pp_\<Theta> rsp\<^sub>0 a v\<^sub>0 large_avail_ret ts_ret pc_ret))\<close>
proof -
  (* Boilerplate code for setting up the master blocks: *)
  note 1 = order_trans[OF _ assms(6)]
  note 2 = word_sub_le[OF 1]
  have \<open>master blocks (rsp\<^sub>0 - 8, 8) 2\<close>
       \<open>master blocks (rsp\<^sub>0 - 12, 1) 2\<close>
       \<open>master blocks (buddy_lists, BUDDY_LISTS * 8) 3\<close>
       \<open>master blocks (a, 1) 4\<close>
       \<open>master blocks (rsp\<^sub>0, 8) 5\<close>
    by (find_master' assms: assms add: 1 2)+
  note masters = this[simplified]

  have rsp_plus_eight_not_leq_rsp_minus_eleven: \<open>rsp\<^sub>0 + 8 \<le> rsp\<^sub>0 - 11 \<longleftrightarrow> False\<close>
  proof -
    have \<open>rsp\<^sub>0 - 11 < rsp\<^sub>0 + 8\<close>
      by (word_strict_trans assms: assms(6,7))
    then show ?thesis
      by simp
  qed

  show ?thesis
    (* Boilerplate code  to start the VCG *)
    apply (rule until_ret.floyd_invarI)
    apply (rewrite at \<open>until_ret.floyd_vcs ts_ret pc_ret \<hole> _\<close> pp_\<Theta>_def)
    apply (intro until_ret.floyd_vcsI; clarsimp?)

    (* Subgoal for (ts, pc) = (0, 0) to (0, 7) *)
    subgoal premises prems for \<sigma>
    proof -
      have rsp_plus_eight_not_leq_rsp_minus_eleven': \<open>RSP \<sigma> + 8 \<le> RSP \<sigma> - 11 \<longleftrightarrow> False\<close>
        using rsp_plus_eight_not_leq_rsp_minus_eleven prems(4)
        by simp

      show ?thesis
        (* Insert relevant knowledge *)
        apply (insert prems assms 1 2)
        (* Apply VCG/symb. execution *)
        apply (symbolic_execution masters: masters) (* push *)
        apply (symbolic_execution masters: masters) (* mov *)
        apply (symbolic_execution masters: masters) (* mov *)
        apply (symbolic_execution masters: masters) (* mov *)
        apply (symbolic_execution masters: masters) (* jmp label_11 *)
        apply (finish_symbolic_execution masters: masters add: rsp_plus_eight_not_leq_rsp_minus_eleven')
        done
    qed

    (* Subgoal for (ts, pc) = (0, 8) to (0, 7) and (ts_ret, pc_ret - 1) *)
    subgoal premises prems for \<sigma>
      (* Insert relevant knowledge *)
      apply (insert prems assms 1 2)
      (* Apply VCG/symb. execution *)
      apply (symbolic_execution masters: masters) (* cmp *)
      apply (symbolic_execution masters: masters del: imp_disjL) (* ja label_13 *)
       apply (symbolic_execution masters: masters) (* cmp *)
       apply (symbolic_execution masters: masters) (* setne *)
        apply (symbolic_execution masters: masters) (* movzx *)
        apply (symbolic_execution masters: masters) (* pop *)
        apply (symbolic_execution masters: masters) (* ret *)

       (* continuing from setne *)
       apply restart_symbolic_execution
       apply (symbolic_execution masters: masters) (* movzx *)
       apply (symbolic_execution masters: masters) (* pop *)
       apply (symbolic_execution masters: masters) (* ret *)

      (* ja label_13 condition not met *)
      apply (restart_symbolic_execution del: imp_disjL)
      apply (symbolic_execution masters: masters) (* movzx *)
      apply (symbolic_execution masters: masters) (* sub *)
      apply (symbolic_execution masters: masters) (* cdqe *)
      apply (symbolic_execution masters: masters) (* lea *)
      apply (symbolic_execution masters: masters) (* lea *)
      apply (symbolic_execution masters: masters) (* mov *)
      apply (symbolic_execution masters: masters) (* test *)
      apply (symbolic_execution masters: masters) (* je label_12 *)
       apply (symbolic_execution masters: masters) (* movzx *)
       apply (symbolic_execution masters: masters) (* add *)
       apply (symbolic_execution masters: masters) (* mov *)
       apply (finish_symbolic_execution masters: masters add: rsp_plus_eight_not_leq_rsp_minus_eleven)

      (* je label_12 condition not met *)
      apply restart_symbolic_execution
      apply (symbolic_execution masters: masters) (* cmp *)
      apply (symbolic_execution masters: masters) (* setne *)
       apply (symbolic_execution masters: masters) (* movzx *)
       apply (symbolic_execution masters: masters) (* pop *)
       apply (symbolic_execution masters: masters) (* ret *)

      (* continuing from setne *)
      apply restart_symbolic_execution
      apply (symbolic_execution masters: masters) (* movzx *)
      apply (symbolic_execution masters: masters) (* pop *)
      apply (symbolic_execution masters: masters) (* ret *)
      done
    done
qed

end

end
